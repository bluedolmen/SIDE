-- @atlcompiler atl2006
module FreeMind; -- Module Template
create OUT : FreeMind from IN : RWM;

helper context String def : normalize() : String =
	self.regexReplaceAll('<([a-zA-Z0-9]*)>(.*)</\\1>','$2').regexReplaceAll('<([a-zA-Z0-9]*)>','')
	    .regexReplaceAll('&lt;([a-zA-Z0-9]*)>(.*)&lt;/\\1>','$2').regexReplaceAll('&lt;([a-zA-Z0-9]*)>','')
		.regexReplaceAll('\\\\(.)','$1');

rule definition {
  from s: RWM!RequirementsDefinition
  to t : FreeMind!Maps (
	  maps <- RWM!Agent->allInstances()
  	)
}

rule agent {
	from s : RWM!Agent
	to t : FreeMind!"Map" (
	  node <- t_node,
	  version <- '1.0'
  	),
	t_node : FreeMind!Node (
		text <- s.name,
		node <- s.isResponsible->collect(goal | thisModule.createNodeFromGoal(goal)),
		folded <- false
	)
}

rule createNodeFromGoal(s : RWM!Goal) {
	to t : FreeMind!Node (
		text <- s.name,
		node <- Sequence{description}->union(s.subgoals->collect(goal | thisModule.createNodeFromGoal(goal))),
		folded <- true
	),
	description : FreeMind!Node (
		text <- s.documentation.normalize(),
		font <- Sequence{font_italic}
	),
	font_italic : FreeMind!Font (
		bold <- false,
		italic <- true,
		size <- 10,
		name <- 'SansSerif'
	)
	do {
		t;
	}
}