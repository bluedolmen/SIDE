-- @atlcompiler atl2006
module RWM2DocBook; -- Module Template
create OUT : DocBook from IN : RWM;

helper context String def : normalize() : String =
	self.regexReplaceAll('<([a-zA-Z0-9]*)>(.*)</\\1>','$2').regexReplaceAll('<([a-zA-Z0-9]*)>','')
	    .regexReplaceAll('&lt;([a-zA-Z0-9]*)>(.*)&lt;/\\1>','$2').regexReplaceAll('&lt;([a-zA-Z0-9]*)>','')
		.regexReplaceAll('\\\\(.)','$1');

helper context RWM!Goal def : getAllSubGoals() : Sequence(RWM!Goal) =
	Sequence{self}->union(self.subgoals->collect(goal | goal.getAllSubGoals())->flatten());

rule createLinkItem(element : RWM!BasicElement) {
	to t : DocBook!ItemizedListValueItem(
		values <- Sequence{t_value,t_xref,t_value2,t_description}
	),
	t_value : DocBook!TextualValue (
		value <- element.name +' ( voir'
	),
	t_xref : DocBook!XRefValue (
		linkend <- element
	),
	t_value2 : DocBook!TextualValue (
		value <- ')'
	),
	t_description : DocBook!EmphasisValue (
		value <- '(' + element.documentation.normalize() + ')'
	)
	do {
		t;
	}
}

rule createLinkList(elements : Sequence(RWM!BasicElement), _mark : String) {
	to t : DocBook!ItemizedListValue(
		mark <- _mark,
		items <- elements->collect(e | thisModule.createLink(e))
	)
	do {
		t;
	}
}

rule createLink(element : RWM!BasicElement) {
	to t : DocBook!Paragraph(
		values <- Sequence{t_value,t_xref,t_value2,t_description}
	),
	t_value : DocBook!TextualValue (
		value <- element.name +' ( voir'
	),
	t_xref : DocBook!XRefValue (
		linkend <- element
	),
	t_value2 : DocBook!TextualValue (
		value <- ')'
	),
	t_description : DocBook!EmphasisValue (
		value <- '(' + element.documentation.normalize() + ')'
	)
	do {
		t;
	}
}

rule createLinkWithoutParagraph(element : RWM!BasicElement) {
	to t_value : DocBook!TextualValue (
		value <- element.name +' ( voir'
	),
	t_xref : DocBook!XRefValue (
		linkend <- element
	),
	t_value2 : DocBook!TextualValue (
		value <- ')'
	),
	t_description : DocBook!EmphasisValue (
		value <- '(' + element.documentation.normalize() + ')'
	)
	do {
		Sequence{t_value,t_xref,t_value2,t_description};
	}
}

rule ReqDefs {
	from s:RWM!RequirementsDefinition
	to t:DocBook!Book (
		bookinfo <- t_bookinfo,
		preface <- t_preface,
		content <- Sequence{t_chapter_agents,t_chapter_goals,t_chapter_entities}
	),
	t_bookinfo:DocBook!BookInfo (
		title <- 'Definition des besoins'
	),
	t_preface:DocBook!Preface (
		para <- Sequence{t_preface_part}
	),
	t_preface_part:DocBook!Paragraph (
		values <- Sequence{t_preface_part_value}
	),
	t_preface_part_value:DocBook!TextualValue (
		value <- 'Ce document a pour objectif de resumer l\'ensembles des besoins du futur outil metier.'
	),
	t_chapter_agents:DocBook!Chapter (
		id <- 'agentList',
		title <- 'Liste des agents',
		titleAbbrev <- 'Liste des agents',
		section <- RWM!Agent->allInstances()
	),
	t_chapter_goals:DocBook!Chapter (
		id <- 'goalList',
		title <- 'Liste des objectifs',
		titleAbbrev <- 'Liste des objectifs',
		section <- RWM!Goal->allInstances()
	),
	t_chapter_entities:DocBook!Chapter (
		id <- 'entityList',
		title <- 'Liste des entites',
		titleAbbrev <- 'Liste des entites',
		section <- RWM!Entity->allInstances()
	)
}

rule Member {
	from s:RWM!Agent 
	to t:DocBook!Section (
		id <- s.name.regexReplaceAll(' ','_').regexReplaceAll('\'','_'),
		title <- s.name,
		titleAbbrev <- s.name,
		level <- 1,
		section <- Sequence{t_section_description, t_section_responsibilities,t_section_privileges}
	),
	t_section_description:DocBook!Section (
		title <- 'Documentation',
		level <- 2,
		para <- Sequence{t_para_description, t_para_description_isHuman}
	),
	t_para_description:DocBook!Paragraph (
		values <- Sequence{t_para_description_value}
	),
	t_para_description_value:DocBook!TextualValue (
		value <- s.documentation.normalize()
	),
	t_para_description_isHuman:DocBook!Paragraph (
		values <- Sequence{t_para_description_isHuman_value1,t_para_description_isHuman_value2}
	),
	t_para_description_isHuman_value1:DocBook!EmphasisValue (
		value <- 'Est un agent humain : ',
		role <- 'bold'
	),
	t_para_description_isHuman_value2:DocBook!EmphasisValue (
		value <- if (s.isHuman) then
				   'Oui'
				 else
				   'Non'
				 endif
	),
	t_section_responsibilities:DocBook!Section (
		title <- 'Responsabilite(s)',
		level <- 2,
		para <- Sequence{t_para_responsibilities}
	),
	t_para_responsibilities:DocBook!Paragraph (
		values <- Sequence{t_para_responsibilities_list_value,t_para_responsibilities_list}
	),
	t_para_responsibilities_list_value:DocBook!TextualValue (
		value <- 'A la responsabilite de : '
	),
	t_para_responsibilities_list:DocBook!ItemizedListValue (
		mark <- 'opencircle',
		items <- s.isResponsible->collect(g | thisModule.createLinkItem(g))
	),
	t_section_privileges:DocBook!Section (
		title <- 'Privilege(s)',
		level <- 2,
		para <- Sequence{t_para_privileges}
	),
	t_para_privileges:DocBook!Paragraph (
		values <- Sequence{t_para_privileges_value}
	),
	t_para_privileges_value:DocBook!InformalTableValue (
		tgroup <- t_group
	),
	t_group:DocBook!InformalTableValueGroup (
		cols <- 5,
		colspec <- Sequence{t_colspec},
		thead <- t_thead,
		tbody <- t_tbody
	),
	t_colspec:DocBook!InformalTableValueColSpec (
		colnum <- 1,
		colwidth <- '4*'
	),
	t_thead:DocBook!InformalTableValueHead (
		rows <- Sequence {t_row}
	),
	t_row:DocBook!InformalTableValueRow (
		entry <- Sequence{t_entry_element, t_entry_create, t_entry_read, t_entry_update, t_entry_delete}
	),
	t_entry_element:DocBook!InformalTableValueEntry(
		value <- 'Element'
	), 
	t_entry_create:DocBook!InformalTableValueEntry(
		value <- 'Creer',
		align <- 'center'
	), 
	t_entry_read:DocBook!InformalTableValueEntry(
		value <- 'Lire',
		align <- 'center'
	), 
	t_entry_update:DocBook!InformalTableValueEntry(
		value <- 'Mettre a jour',
		align <- 'center'
	), 
	t_entry_delete:DocBook!InformalTableValueEntry(
		value <- 'Supprimer',
		align <- 'center'
	),
	t_tbody:DocBook!InformalTableValueBody (
		rows <- let allPrivileges : Sequence(RWM!BasicElement) = 
			              s.isResponsible->collect(goal | goal.getAllSubGoals())->flatten()->collect(goal | goal.privilegeGroup)->excluding(OclUndefined)->collect(pGroup | pGroup.privileges)->flatten()
						in 
		                let allPrivilegesElement : Sequence(RWM!BasicElement) = 
			              allPrivileges->collect(p | p.element)->flatten()
						in
						  let 	entities : Sequence(RWM!Entity) = 
		 					allPrivilegesElement->select(e | e.oclIsTypeOf(RWM!Entity))->
				            union( allPrivilegesElement->select(e | e.oclIsTypeOf(RWM!Attribute))->collect(e | e.refImmediateComposite()))->
				            asSet()->asSequence()
				          in
				             entities->iterate(entity; all : Sequence(RWM!BasicElement) = Sequence{} | all->append(entity)->union(entity.attributes)->union(RWM!RelationShip->allInstances()->select(r | r.ends->collect(e|e.object)->includes(entity))))
				             ->collect(e | thisModule.Privilege(e,allPrivileges,OclUndefined))
	)
	do {
	}
}

rule Goal {
	from s:RWM!Goal 
	to t:DocBook!Section (
		id <- s.name.regexReplaceAll(' ','_').regexReplaceAll('\'','_'),
		title <- s.name,
		titleAbbrev <- s.name,
		level <- 1,
		section <- Sequence{t_section_description,t_section_privileges}
	),
	t_section_description:DocBook!Section (
		title <- 'Documentation',
		level <- 2,
		para <- Sequence{t_para_description,t_para_description_level,t_para_description_parent,t_para_description_child}
	),
	t_para_description:DocBook!Paragraph (
		values <- Sequence{t_para_description_value}
	),
	t_para_description_value:DocBook!TextualValue (
		value <- s.documentation.normalize()
	),
	t_para_description_level:DocBook!Paragraph (
		values <- Sequence{t_para_description_level_value1,t_para_description_level_value2}
	),
	t_para_description_level_value1:DocBook!EmphasisValue (
		value <- 'Niveau d\'importance : ',
		role <- 'bold'
	),
	t_para_description_level_value2:DocBook!EmphasisValue (
		value <- s.priority.toString()
	),
	t_para_description_parent:DocBook!Paragraph (
		values <- if (RWM!Goal->allInstances()->select(g | g.subgoals->includes(s))->size() > 0) then
		            thisModule.createLinkWithoutParagraph(RWM!Goal->allInstances()->select(g | g.subgoals->includes(s))->first())->insertAt(1,t_para_description_parent_value)
				  else
				  	Sequence{t_para_description_parent_value}
		          endif
	),
	t_para_description_parent_value:DocBook!EmphasisValue (
		value <- 'Objectif parent : ',
		role <- 'bold'
	),
	t_para_description_child:DocBook!Paragraph (
		values <- if (s.subgoals->size() > 0) then 
		            Sequence{t_para_description_child_list_value,thisModule.createLinkList(s.subgoals,'opencircle')}
		          else
		          	Sequence{t_para_description_child_list_value}
		          endif
	),
	t_para_description_child_list_value:DocBook!EmphasisValue (
		value <- 'Liste des sous-objectifs : ',
		role <- 'bold'
	),
	t_section_privileges:DocBook!Section (
		title <- 'Privileges',
		level <- 2,
		para <- Sequence{t_para_privileges}
	),
	t_para_privileges:DocBook!Paragraph (
		values <- Sequence{s.privilegeGroup}
	)
}

rule GoalWithResponsible extends Goal{
	from s:RWM!Goal (s.responsible.size() > 0)
	to t:DocBook!Section (
		section <- Sequence{t_section_description, t_section_responsibilities,t_section_privileges}
	),
	t_section_responsibilities:DocBook!Section (
		title <- 'Responsabilite(s)',
		level <- 2,
		para <- Sequence{t_para_responsibilities}
	),
	t_para_responsibilities:DocBook!Paragraph (
		values <- Sequence{t_para_responsibilities_list_value, t_para_responsibilities_list}
	),
	t_para_responsibilities_list_value:DocBook!TextualValue (
		value <- 'Sous la responsabilite de : '
	),
	t_para_responsibilities_list:DocBook!ItemizedListValue (
		mark <- 'opencircle',
		items <- s.responsible->collect(a | thisModule.createLinkItem(a))
	)
}

rule PrivilegeGroup {
	from s:RWM!PrivilegeGroup(s.privileges.size() > 0)
	to t:DocBook!InformalTableValue (
		tgroup <- t_group
	),
	t_group:DocBook!InformalTableValueGroup (
		cols <- 5,
		colspec <- Sequence{t_colspec},
		thead <- t_thead,
		tbody <- t_tbody
	),
	t_colspec:DocBook!InformalTableValueColSpec (
		colnum <- 1,
		colwidth <- '4*'
	),
	t_thead:DocBook!InformalTableValueHead (
		rows <- Sequence {t_row}
	),
	t_row:DocBook!InformalTableValueRow (
		entry <- Sequence{t_entry_element, t_entry_create, t_entry_read, t_entry_update, t_entry_delete}
	),
	t_entry_element:DocBook!InformalTableValueEntry(
		value <- 'Element'
	), 
	t_entry_create:DocBook!InformalTableValueEntry(
		value <- 'Creer',
		align <- 'center'
	), 
	t_entry_read:DocBook!InformalTableValueEntry(
		value <- 'Lire',
		align <- 'center'
	), 
	t_entry_update:DocBook!InformalTableValueEntry(
		value <- 'Mettre a jour',
		align <- 'center'
	), 
	t_entry_delete:DocBook!InformalTableValueEntry(
		value <- 'Supprimer',
		align <- 'center'
	),
	t_tbody:DocBook!InformalTableValueBody (
		rows <- let entities : Sequence(RWM!Entity) = 
		 		s.privileges->collect(p | p.element)->select(e | e.oclIsTypeOf(RWM!Entity))->
				union( s.privileges->collect(p | p.element)->select(e | e.oclIsTypeOf(RWM!Attribute))->collect(e | e.refImmediateComposite()) )->
				asSet()->asSequence()
				in
				  entities->iterate(entity; all : Sequence(RWM!BasicElement) = Sequence{} | all->append(entity)->union(entity.attributes)->union(RWM!RelationShip->allInstances()->select(r | r.ends->collect(e|e.object)->includes(entity))))
				  ->collect(e | thisModule.Privilege(e,s.privileges,s.entryPoint))
	)
}

rule Privilege(element : RWM!BasicElement, group : Sequence(RWM!Privilege), entryPoint : RWM!BasicElement)  {
	to t:DocBook!InformalTableValueRow (
	  entry <- Sequence{t_entry_element, t_entry_create, t_entry_read, t_entry_update, t_entry_delete}
	),
	t_entry_element:DocBook!InformalTableValueEntry(
		value <- if (element.oclIsTypeOf(RWM!Entity)) then
		           if (element = entryPoint) then
				     '&gt; '+element.name
				   else
		             element.name
				   endif
				 else
				   if (element.oclIsTypeOf(RWM!Attribute)) then
				     element.refImmediateComposite().name+' - [A] '+element.name
				   else
				     '[R] '+element.name
				   endif
				 endif
	), 
	t_entry_create:DocBook!InformalTableValueEntry(
		value <- if (group->select(p | p.element = element and p.category = #"create")->size() > 0) then
			       '&times;'
			     else
			       ''
				 endif,
		align <- 'center'
	), 
	t_entry_read:DocBook!InformalTableValueEntry(
		value <- if (group->select(p | p.element = element and p.category = #read)->size() > 0) then
			       '&times;'
			     else
			       ''
				 endif,
		align <- 'center'
	), 
	t_entry_update:DocBook!InformalTableValueEntry(
		value <- if (group->select(p | p.element = element and p.category = #update)->size() > 0) then
			       '&times;'
			     else
			       ''
				 endif,
		align <- 'center'
	), 
	t_entry_delete:DocBook!InformalTableValueEntry(
		value <- if (group->select(p | p.element = element and p.category = #delete)->size() > 0) then
			       '&times;'
			     else
			       ''
				 endif,
		align <- 'center'
	)
	do {
		t;
	}
}

rule Entity {
	from s:RWM!Entity 
	to t:DocBook!Section (
		id <- s.name.regexReplaceAll(' ','_').regexReplaceAll('\'','_'),
		title <- s.name,
		titleAbbrev <- s.name,
		level <- 1,
		section <- Sequence{t_section_description,t_section_relationship}
	),
	t_section_description:DocBook!Section (
		title <- 'Documentation',
		level <- 2,
		para <- Sequence{t_para_description}
	),
	t_para_description:DocBook!Paragraph (
		values <- Sequence{t_para_description_value}
	),
	t_para_description_value:DocBook!TextualValue (
		value <- s.documentation.normalize()
	),
	t_section_relationship:DocBook!Section (
		title <- 'Relation(s)',
		level <- 2,
		para <- Sequence{t_para_relationship}
	),
	t_para_relationship:DocBook!Paragraph (
		values <-if (RWM!End->allInstances()->select(end | end.object = s)->size() > 0) then
		                   Sequence{t_para_relationship_list_value,thisModule.RelationShip(s)}
		                 else
		                   Sequence{t_para_relationship_list_value}
		                 endif
	),
	t_para_relationship_list_value:DocBook!TextualValue (
		value <- 'Liste des relations : '
	)
}

rule RelationShip(s : RWM!Entity) {
	to t_para_relationship_list:DocBook!ItemizedListValue (
		mark <- 'opencircle',
		items <- RWM!End->allInstances()->select(end | end.object = s)->collect(end | end.refImmediateComposite())->collect(r | r.ends)->flatten()->select(end | end.object <> s)->collect(end | thisModule.createLinkItem(end.object))
	)
	do {
		t_para_relationship_list;
	}
}

rule EntityWithAttribute extends Entity {
	from s:RWM!Entity (s.attributes.size() > 0)
	to t:DocBook!Section (
		section <- Sequence{t_section_description,t_section_attributes,t_section_relationship}
	),
	t_section_attributes:DocBook!Section (
		title <- 'Attribut(s)',
		level <- 2,
		para <- Sequence{t_para_attributs}
	),
	t_para_attributs:DocBook!Paragraph (
		values <- Sequence{t_para_attributs_table}
	),
	t_para_attributs_table:DocBook!InformalTableValue (
		tgroup <- t_group
	),
	t_group:DocBook!InformalTableValueGroup (
		cols <- 3,
		colspec <- Sequence{t_colspec},
		thead <- t_thead,
		tbody <- t_tbody
	),
	t_colspec:DocBook!InformalTableValueColSpec (
		colnum <- 3,
		colwidth <- '4*'
	),
	t_thead:DocBook!InformalTableValueHead (
		rows <- Sequence {t_row}
	),
	t_row:DocBook!InformalTableValueRow (
		entry <- Sequence{t_entry_name, t_entry_type,t_entry_description}
	),
	t_entry_name:DocBook!InformalTableValueEntry(
		value <- 'Nom'
	), 
	t_entry_type:DocBook!InformalTableValueEntry(
		value <- 'Type',
		align <- 'center'
	), 
	t_entry_description:DocBook!InformalTableValueEntry(
		value <- 'Description'
	), 
	t_tbody:DocBook!InformalTableValueBody (
		rows <- s.attributes
	)
}

rule Attribute {
	from s:RWM!Attribute
	to t:DocBook!InformalTableValueRow (
	  entry <- Sequence{t_entry_name, t_entry_type,t_entry_description}
	),
	t_entry_name:DocBook!InformalTableValueEntry(
		value <- s.name
	), 
	t_entry_type:DocBook!InformalTableValueEntry(
		value <- s."type".toString(),
		align <- 'center'
	), 
	t_entry_description:DocBook!InformalTableValueEntry(
		value <- s.documentation.normalize()
	)
}